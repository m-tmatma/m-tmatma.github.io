<html>
<head>
<!-- Start Google Analytics -->
<script async type="text/javascript" src="../googleanalytics.js"></script>
<!-- End Google Analytics -->
<meta name="google-site-verification" content="8Y9FzGJmBS8lXf2-5dS9kvKDfT7uJFyN2AF63K5LgJQ" />
<meta name="viewport" content="width=device-width,initial-scale=1">
<meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
<link href="../style.css" type="text/css" rel="stylesheet" />
<title>cmake での shared library/static library のテストプロジェクト</title>
<meta name="keywords" content="cmake" />
<meta name="keywords" content="foreach" />
<meta name="keywords" content="LISTS" />
<meta name="keywords" content="SOURCE_DIR" />
<meta name="keywords" content="BINARY_DIR" />
</head>
<body>
<div class="ads_top">
	<div class="responsive_googleads_top">
	</div>
	<!-- end of "responsive_googleads_top" -->
</div>
<!-- end of "ads_top" -->



<div class="index">
	<a href="../sitemap.html">サイトマップ</a>
	<a href="../contact.html">連絡先</a>
	<a href="../">最初に戻る</a>
	<a href="cmake-compile.html">戻る</a>
	<a href="cmake-googletest-sample.html">次へ進む</a>
</div>
<div align="right">
$Date: 2018-07-07 06:49:13 +0900 (2018/07/07 (土)) $<br>
$Revision: 1347 $<br>
</div>

<h1>cmake での shared library/static library のテストプロジェクト</h1>

<h2>概要</h2>

shared library(DLL) と static library を使う場合をプロジェクト生成時(cmake 実行時) に動的に切り替えられる方法を説明します。

<h2>サンプルのプロジェクトの構成</h2>

<pre>
│  CMakeLists.txt
│
├─Library
│      CMakeLists.txt
│      Library.c
│      Library.h
│
└─Program
        CMakeLists.txt
        Program.c
</pre>

<a href="https://github.com/m-tmatma/cmake-sample.git">https://github.com/m-tmatma/cmake-sample.git</a> の
LibrarySample ディレクトリに格納されています。

<h2>library のプロジェクトの定義</h2>

Library/CMakeLists.txt

<pre>
cmake_minimum_required(VERSION 2.6.4)

# define a variable of project name
set( project_name "Library")

# define a project name
project (${project_name})

# define a variable SRC with file GLOB
file(GLOB SRC *.c *.h)

# add include directory for ${project_name}_Export.h
include_directories(${PROJECT_BINARY_DIR})

# define sources files of an executable
add_library(${project_name} ${LIB_TYPE} ${SRC} ${PROJECT_BINARY_DIR}/${project_name}_Export.h)

include (GenerateExportHeader)
GENERATE_EXPORT_HEADER( ${project_name}
	BASE_NAME ${project_name}
	EXPORT_MACRO_NAME ${project_name}_EXPORT
	EXPORT_FILE_NAME ${project_name}_Export.h
	STATIC_DEFINE ${project_name}_BUILT_AS_STATIC
)
</pre>

include_directories でインクルードディレクトリを指定します<br>
${PROJECT_BINARY_DIR} はこのプロジェクトに対応するビルドディレクトリを意味します。<br>

add_library でライブラリを定義します。${LIB_TYPE} のところで SHARED を指定すると shared Library(Windows ではDLL)になり,<br>
STATIC を指定するとスタティックライブラリになります。<br>

トップディレクトリにある CMakeLists.txt でこの変数を定義します。<br>

以下の指定では Library_Export.h というDLL のインポート、エクスポート指定のためのヘッダファイルを作成するための命令です。<br>

<pre>
include (GenerateExportHeader)
GENERATE_EXPORT_HEADER( ${project_name}
	BASE_NAME ${project_name}
	EXPORT_MACRO_NAME ${project_name}_EXPORT
	EXPORT_FILE_NAME ${project_name}_Export.h
	STATIC_DEFINE ${project_name}_BUILT_AS_STATIC
)
</pre>

以下のようなヘッダファイルが出力先に生成されます。<br>
このファイルをインクルードするために include_directories(${PROJECT_BINARY_DIR}) の指定をしています。<br>

<pre>
#ifndef Library_EXPORT_H
#define Library_EXPORT_H

#ifdef Library_BUILT_AS_STATIC
#  define Library_EXPORT
#  define LIBRARY_NO_EXPORT
#else
#  ifndef Library_EXPORT
#    ifdef Library_EXPORTS
        /* We are building this library */
#      define Library_EXPORT __declspec(dllexport)
#    else
        /* We are using this library */
#      define Library_EXPORT __declspec(dllimport)
#    endif
#  endif

#  ifndef LIBRARY_NO_EXPORT
#    define LIBRARY_NO_EXPORT 
#  endif
#endif

#ifndef LIBRARY_DEPRECATED
#  define LIBRARY_DEPRECATED __declspec(deprecated)
#endif

#ifndef LIBRARY_DEPRECATED_EXPORT
#  define LIBRARY_DEPRECATED_EXPORT Library_EXPORT LIBRARY_DEPRECATED
#endif

#ifndef LIBRARY_DEPRECATED_NO_EXPORT
#  define LIBRARY_DEPRECATED_NO_EXPORT LIBRARY_NO_EXPORT LIBRARY_DEPRECATED
#endif

#if 0 /* DEFINE_NO_DEPRECATED */
#  ifndef LIBRARY_NO_DEPRECATED
#    define LIBRARY_NO_DEPRECATED
#  endif
#endif

#endif
</pre>

<h2>exe のプロジェクトの定義</h2>

Program/CMakeLists.txt

<pre>
cmake_minimum_required(VERSION 2.6.4)

# define a variable of project name
set( project_name sample_program)

# set the project as the startup project
set_property(DIRECTORY PROPERTY VS_STARTUP_PROJECT ${project_name} )

# define a project name
project (${project_name})

# define a variable SRC with file GLOB
file(GLOB SRC *.c *.h)

# add include directory
# https://cmake.org/cmake/help/v3.0/variable/PROJECT-NAME_SOURCE_DIR.html
include_directories(${Library_SOURCE_DIR})

# add include directory for ${project_name}_Export.h
# https://cmake.org/cmake/help/v3.0/variable/PROJECT-NAME_BINARY_DIR.html
include_directories(${Library_BINARY_DIR})

# define sources files of an executable
add_executable(${project_name} ${SRC})

# link a library
target_link_libraries(${project_name} Library)
</pre>

<h3>古い説明</h3>

<strike>
<p>
include_directories でインクルードディレクトリの指定をしていますが、
${PROJECT_BINARY_DIR} を参照する場合、Program のプロジェクトに対する生成ディレクトリなので
Library のプロジェクトに対するフォルダを指定する必要があるので ../Library を追加しています。
</p>
</strike>

<h3>&lt;PROJECT-NAME&gt;_SOURCE_DIR と &lt;PROJECT-NAME&gt;_BINARY_DIR</h3>

<table>
<tr>
	<th>変数名</th>
	<th>リンク</th>
	<th>説明</th>
</tr>
<tr>
	<td>&lt;PROJECT-NAME&gt;_SOURCE_DIR</td>
	<td>
	<a href="https://cmake.org/cmake/help/v3.0/variable/PROJECT-NAME_SOURCE_DIR.html">
	&lt;PROJECT-NAME&gt;_SOURCE_DIR
	</a>
	</td>
	<td>&lt;PROJECT-NAME&gt; のプロジェクトのソースコードのトップディレクトリ</td>
</tr>
<tr>
	<td>&lt;PROJECT-NAME&gt;_BINARY_DIR</td>
	<td>
	<a href="https://cmake.org/cmake/help/v3.0/variable/PROJECT-NAME_BINARY_DIR.html">
	&lt;PROJECT-NAME&gt;_BINARY_DIR
	</a>
	</td>
	<td>&lt;PROJECT-NAME&gt; のプロジェクトのバイナリディレクトリ</td>
</tr>
</table>

<p>
&lt;PROJECT-NAME&gt; は project() で指定する名前です。
</p>

<p>
例えばある CMakeLists.txt で以下のように定義しているプロジェクトを参照する場合
&lt;PROJECT-NAME&gt;_SOURCE_DIR は ${calc_SOURCE_DIR} のように参照します。
</p>

<pre>
# define a variable of project name
set( project_name calc)

# define a variable of project name
project (${project_name})
</pre>

<h3>include_directories</h3>

<table>
<tr>
	<th>記述</th>
	<th>説明</th>
</tr>
<tr>
	<td>
	include_directories(<strong>${Library_SOURCE_DIR}</strong>/include)
	</td>
	<td>
	Library プロジェクトのソースコードのディレクトリをインクルードディレクトリに追加する
	</a>
	</td>
</tr>
<tr>
	<td>
	include_directories(<strong>${Library_BINARY_DIR}</strong>)
	</td>
	<td>
	Library プロジェクトのバイナリディレクトリをインクルードディレクトリに追加する
	</a>
	</td>
</tr>
</table>

<h3>入れ子の変数定義/foreach</h3>

以下のように変数 external_proj を定義して値として Library を入れておいて
${${external_proj}_SOURCE_DIR} で ${Library_SOURCE_DIR} を参照することができます。

<pre>
set(external_proj Library)
include_directories(${${external_proj}_SOURCE_DIR})
</pre>

このことを利用すると
<a href="https://cmake.org/cmake/help/v3.0/command/foreach.html">
foreach
</a>
を使うと、複数のプロジェクトのソースディレクトリやバイナリディレクトリに対して
一括で指定ができます。

<pre>
#↓ 以下のように定義してもよい
#set(external_projs Library Library2)

#↓ 以下のように定義してもよい
set(<strong>external_projs</strong> ${external_projs} Library)
set(<strong>external_projs</strong> ${external_projs} Library2)

foreach(<strong>loop_var</strong> IN LISTS <strong>external_projs</strong>)
	# add include directory
	# https://cmake.org/cmake/help/v3.0/variable/PROJECT-NAME_SOURCE_DIR.html
	include_directories(${${<strong>loop_var</strong>}_SOURCE_DIR})

	# add include directory for ${project_name}_Export.h
	# https://cmake.org/cmake/help/v3.0/variable/PROJECT-NAME_BINARY_DIR.html
	include_directories(${${<strong>loop_var</strong>}_BINARY_DIR})
endforeach()


# define sources files of an executable
# <strong>target_link_libraries() は add_executable より後に指定します。</strong>
add_executable(${project_name} ${SRC})

# link a library
foreach(<strong>loop_var</strong> IN LISTS <strong>external_projs</strong>)
	target_link_libraries(${project_name} ${<strong>loop_var</strong>})
endforeach()
</pre>

<h3>target_link_libraries</h3>

<p>
target_link_libraries でリンクするライブラリを指定しています。Library をリンクしています。
</p>

<h2>トップのプロジェクトの定義</h2>

CMakeLists.txt

<pre>
cmake_minimum_required(VERSION 2.6.4)

# see https://cmake.org/Wiki/BuildingWinDLL
# Allow the developer to select if Dynamic or Static libraries are built
option (BUILD_SHARED_LIBS "Build Shared Libraries" ON)
# Set the LIB_TYPE variable to STATIC
set (LIB_TYPE STATIC)
if (BUILD_SHARED_LIBS)
  # User wants to build Dynamic Libraries, so change the LIB_TYPE variable to CMake keyword 'SHARED'
  set (LIB_TYPE SHARED)
endif (BUILD_SHARED_LIBS)

project (LibraryTest)

set_property(DIRECTORY PROPERTY VS_STARTUP_PROJECT "sample_program" )

set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)

add_subdirectory(Library)
add_subdirectory(Program)

# turn on solution folder
set_property(GLOBAL PROPERTY USE_FOLDERS ON)

# create solution folder
set_target_properties(Library        PROPERTIES FOLDER library)
set_target_properties(sample_program PROPERTIES FOLDER executable)
</pre>

以下定義で BUILD_SHARED_LIBS が ON になっていたら ${LIB_TYPE} は SHARED (Shared Library) になります。<br>

<pre>
option (BUILD_SHARED_LIBS "Build Shared Libraries" ON)
# Set the LIB_TYPE variable to STATIC
set (LIB_TYPE STATIC)
if (BUILD_SHARED_LIBS)
  # User wants to build Dynamic Libraries, so change the LIB_TYPE variable to CMake keyword 'SHARED'
  set (LIB_TYPE SHARED)
endif (BUILD_SHARED_LIBS)
</pre>

BUILD_SHARED_LIBS が OFF になっていたら ${LIB_TYPE} は STATIC (Static Library) になります。<br>
そして BUILD_SHARED_LIBS はコマンドラインで cmake を実行するときに上書きすることができます。<br>

以下のように実行してプロジェクトを生成すれば Shared Library (DLL) になります。<br>

<pre>
cmake -D BUILD_SHARED_LIBS=1 ..\cmake-sample\LibrarySample\
</pre>

以下のように実行してプロジェクトを生成すれば Static Library になります。<br>

<pre>
cmake -D BUILD_SHARED_LIBS=0 ..\cmake-sample\LibrarySample\
</pre>

<p>
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin) でライブラリ出力先フォルダを指定します。<br>
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin) で実行ファイルの出力先フォルダを指定します。<br>
CMAKE_BINARY_DIR の変数はビルドディレクトリのトップディレクトリを表します<br>
</p>

<p>
<a href="http://qiita.com/osamu0329/items/7de2b190df3cfb4ad0ca">CMake : out-of-sourceビルドで幸せになる</a>を参考<br>
</p>

<p>
add_subdirectory(Library) でライブラリ用のディレクトリをビルド対象に追加しています。<br>
</p>

<pre>
# turn on solution folder
set_property(GLOBAL PROPERTY USE_FOLDERS ON)

# create solution folder
set_target_properties(Library        PROPERTIES FOLDER library)
set_target_properties(sample_program PROPERTIES FOLDER executable)
</pre>

set_property(GLOBAL PROPERTY USE_FOLDERS ON) でソリューションフォルダの機能を有効にします。<br>
set_target_properties(sample_program PROPERTIES FOLDER executable) で sample_program というプロジェクトを executable というソリューションフォルダに入れています。<br>



</body>
</html>
